Package br.com.ufs.pascalito;

Helpers 
    digito = ['0' .. '9'];
    letra = ['a' .. 'z'] | ['A' .. 'Z'];
    tab = 9;
    cr = 13;
    lf = 10;
    eol = cr lf | cr | lf;
    blank = (' ' | tab | eol)+;
    ascii = [0 .. 0x7F];
    aspa_simples = ''';
    ascii_sem_newline = [ascii - [cr + lf]];
    ascii_sem_asterisco = [ascii - '*'];
    ascii_sem_asterico_barra = [ascii_sem_asterisco - '/'];
    ascii_sem_aspa_simples_newline = [ascii_sem_newline - aspa_simples];
    
    
    coment_linha = '//' ascii_sem_newline* eol;
    coment_bloco_inicio = '/*';
    
    a = 'a' | 'A';
	b = 'b' | 'B';
	c = 'c' | 'C';
	d = 'd' | 'D';
	e = 'e' | 'E';
	f = 'f' | 'F';
	g = 'g' | 'G';
	h = 'h' | 'H';
	i = 'i' | 'I';
	j = 'j' | 'J';
	k = 'k' | 'K';
	l = 'l' | 'L';
	m = 'm' | 'M';
	n = 'n' | 'N';
	o = 'o' | 'O';
	p = 'p' | 'P';
	q = 'q' | 'Q';
	r = 'r' | 'R';
	s = 's' | 'S';
	t = 't' | 'T';
	u = 'u' | 'U';
	v = 'v' | 'V';
	w = 'w' | 'W';
	x = 'x' | 'X';
	y = 'y' | 'Y';
	z = 'z' | 'Z';
    
States
	normal,
	comment,
	string;

Tokens
{normal}    integer = i n t e g e r;
{normal}    string = s t r i n g;
{normal}    boolean = b o o l e a n;
{normal}    array = a r r a y;
{normal}    if = i f;
{normal}    then = t h e n;
{normal}    else = e l s e;
{normal}    while = w h i l e;
{normal}    for = f o r;
{normal}    case = c a s e;
{normal}    of = o f;
{normal}    begin = b e g i n;
{normal}    end = e n d;
{normal}    program = p r o g r a m;
{normal}    var = v a r;
{normal}	function = f u n c t i o n;
{normal}	label = l a b e l;
{normal}	procedure = p r o c e d u r e;
{normal}	do = d o;
{normal}	type = t y p e;
{normal}	goto = g o t o;

{normal}	const_booleana = t r u e | f a l s e;
{normal}	and = a n d;
{normal}	or = o r;
{normal}	div = d i v;
{normal}	mod = m o d;
{normal}	not = n o t;
{normal}    identificador = letra (letra | digito)*;
{normal}	ponto_e_virgula = ';';
{normal}	ponto = '.';
{normal}	ponto_ponto = '..';
{normal}	abre_parenteses = '(';
{normal}	fecha_parenteses = ')';
{normal}	virgula = ',';
{normal}	atribuicao = ':=';
{normal}	dois_pontos = ':';
{normal}	abre_colchete = '[';
{normal}	fecha_colchete = ']';
{normal}	const_inteiro = digito digito*;
{normal}    blank = blank;
{normal}	menor = '<';
{normal}	menor_igual = '<=';
{normal}	maior = '>';
{normal}	maior_igual = '>=';
{normal}	diferente = '<>';
{normal}	igual = '=';
{normal}	soma = '+';
{normal}	subtracao = '-';
{normal}	multiplicacao = '*';
{normal}	divisao = '/';

{normal}	coment_linha = coment_linha;

{normal->comment, comment}	coment_bloco = coment_bloco_inicio;
{comment, normal}	coment_bloco_fim = '*/';
{comment}	coment_bloco_corpo = ascii_sem_asterico_barra*;
{comment}	asterico = '*';
{comment}	barra = '/';

{normal->string}    const_string = aspa_simples;
{string}	const_string_corpo = ascii_sem_aspa_simples_newline*;
{string}	aspa_simples = aspa_simples;
{string}	newline = eol;

Ignored Tokens
	blank,
	coment_bloco,
	coment_linha;
	
Productions
	program {-> P.program} = {unico} T.program identificador ponto_e_virgula bloco ponto {-> New program(New string(identificador), [], bloco) }
		| {multiplo} T.program identificador abre_parenteses identificadores fecha_parenteses ponto_e_virgula bloco ponto {-> New program(New string(identificador), [identificadores.string], bloco) };
	
	bloco {-> P.bloco} = declaracao_labels declaracao_tipos declaracoes_variaveis declaracoes_proc_funcoes sentenca_composta 
		{-> New bloco([declaracao_labels.numero_sem_sinal], [declaracao_tipos.definicao_tipo], [declaracoes_variaveis.declaracao_variavel], [declaracoes_proc_funcoes.declaracao_proc_ou_funcao]) };
	
	identificadores {-> P.string*} = {unico} identificador {-> [New string(identificador)]} 
		| {multiplo} identificadores virgula identificador {-> [identificadores.string, New string(identificador)]};
	
	identificador_tipo_ordinal {-> tipo_primitivo} = {integer} integer {-> New tipo_primitivo.integer(integer)} 
		| {string} string {-> New tipo_primitivo.string(T.string)} 
		| {boolean} boolean {-> New tipo_primitivo.boolean(boolean)};
	
	identificador_tipo {-> tipo_id_ou_primitivo} = {identificador} identificador {-> New tipo_id_ou_primitivo.id(New tipo_identificador(New string(identificador))) } 
		| {ordinal} identificador_tipo_ordinal {-> New tipo_id_ou_primitivo.primitivo(identificador_tipo_ordinal.tipo_primitivo) };
	
	declaracao_labels {-> P.numero_sem_sinal*} = T.label labels ponto_e_virgula {-> [labels.numero_sem_sinal]}
		| {empty} {-> []};
		
	labels {-> P.numero_sem_sinal*} = {unico} P.label {-> [label.numero_sem_sinal]} 
		| {multiplo} labels virgula P.label {-> [label.numero_sem_sinal, labels.numero_sem_sinal]};
		
	label {-> P.numero_sem_sinal} = const_inteiro {-> New numero_sem_sinal(const_inteiro) };
	
	declaracao_tipos {-> definicao_tipo*} = type definicao_tipos ponto_e_virgula {-> [definicao_tipos.definicao_tipo]} 
		| {empty} {-> []};
	
	definicao_tipos {-> definicao_tipo*} = {unico} definicao_tipo {-> [definicao_tipo]} 
		| {multiplo} definicao_tipos ponto_e_virgula definicao_tipo {-> [definicao_tipo, definicao_tipos.definicao_tipo] };
	
	definicao_tipo {-> definicao_tipo} = identificador igual denotador_tipo {-> New definicao_tipo(New string(identificador), denotador_tipo) };
	
	denotador_tipo {-> denotador_tipo} = {existente} identificador {-> New denotador_tipo.id_ou_ordinal(New tipo_id_ou_ordinal.id(New tipo_identificador(New string(identificador))))}
		| {ordinal} identificador_tipo_ordinal {-> New denotador_tipo.primitivo(identificador_tipo_ordinal.tipo_primitivo) };
	
	novo_tipo = {ordinal} novo_tipo_ordinal | {array} tipo_array;
	
	tipo_array {-> tipo_array} = array abre_colchete tipos_ordinais fecha_colchete of denotador_tipo {-> New tipo_array([tipos_ordinais.tipo_id_ou_ordinal], denotador_tipo)};
	
	tipos_ordinais {-> tipo_id_ou_ordinal*} = {unico} tipo_ordinal {-> [tipo_ordinal.tipo_id_ou_ordinal]}
		| {multiplo} tipos_ordinais virgula tipo_ordinal {-> [tipo_ordinal.tipo_id_ou_ordinal, tipos_ordinais.tipo_id_ou_ordinal]};
		
	tipo_ordinal {-> tipo_id_ou_ordinal} = {novo} novo_tipo_ordinal {-> novo_tipo_ordinal.tipo_id_ou_ordinal} 
		| {existente} identificador {-> New tipo_id_ou_ordinal.id(New tipo_identificador(New string(identificador)))};
		
	novo_tipo_ordinal {-> tipo_id_ou_ordinal} = {enumerado} tipo_enumerado {-> New tipo_id_ou_ordinal.enumerado(tipo_enumerado)}
		| {subrange} tipo_subrange {-> New tipo_id_ou_ordinal.subrange(tipo_subrange)};
		
	tipo_enumerado {-> tipo_enumerado} = abre_parenteses identificadores fecha_parenteses {-> New tipo_enumerado([identificadores.string])};
	
	tipo_subrange {-> tipo_subrange} = [inicio]:constante ponto_ponto [fim]:constante {-> New tipo_subrange(inicio, fim) };
	
	sinal {-> sinal} = {soma} soma {-> New sinal.mais(soma)} 
		| {subtracao} subtracao {-> New sinal.menos(subtracao)};
	
	constante {-> constante} = {numero} sinal? const_inteiro {-> New constante.inteiro(sinal, const_inteiro)} 
		| {string} const_string {-> New constante.string(const_string) }
		| {boolean} const_booleana {-> New constante.boolean(const_booleana) };
	
	constante_sem_sinal {-> constante} = {numero} const_inteiro {-> New constante.inteiro(Null, const_inteiro) } 
		| {string} const_string {-> New constante.string(const_string) }
		| {boolean} const_booleana {-> New constante.boolean(const_booleana) };
	
	declaracoes_variaveis {-> declaracao_variavel*} = var declaracao_variaveis ponto_e_virgula {-> [declaracao_variaveis.declaracao_variavel]} | {empty} {-> []};
	
	declaracao_variaveis {-> declaracao_variavel*} = {unico} declaracao_variavel {-> [declaracao_variavel.declaracao_variavel] }
		| {multiplo} declaracao_variaveis ponto_e_virgula declaracao_variavel {-> [declaracao_variavel.declaracao_variavel, declaracao_variaveis.declaracao_variavel] };
	
	declaracao_variavel {-> declaracao_variavel} = identificadores dois_pontos denotador_tipo {-> New declaracao_variavel([identificadores.string], denotador_tipo) };
	
	declaracoes_proc_funcoes {-> declaracao_proc_ou_funcao*} = declaracao_proc_funcoes {-> [declaracao_proc_funcoes.declaracao_proc_ou_funcao]} | {empty} {-> []};
	
	declaracao_proc_funcoes {-> declaracao_proc_ou_funcao*} = {unico} declaracao_proc_funcao {-> [declaracao_proc_funcao.declaracao_proc_ou_funcao] }
		| {multiplo} declaracao_proc_funcoes declaracao_proc_funcao {-> [declaracao_proc_funcao.declaracao_proc_ou_funcao, declaracao_proc_funcoes.declaracao_proc_ou_funcao] };
	
	declaracao_proc_funcao {-> declaracao_proc_ou_funcao} = {procedure} declaracao_procedure ponto_e_virgula {-> New declaracao_proc_ou_funcao.procedure(declaracao_procedure) } 
		| {funcao} declaracao_funcao ponto_e_virgula {-> New declaracao_proc_ou_funcao.funcao(declaracao_funcao) };
	
	declaracao_procedure {-> P.declaracao_procedure} = procedure identificador parametros_formais ponto_e_virgula bloco {-> New declaracao_procedure(New string(identificador), [parametros_formais.parametro_formal], bloco)};
	
	declaracao_funcao {-> P.declaracao_funcao} = function identificador parametros_formais dois_pontos tipo_resultado ponto_e_virgula bloco {-> New declaracao_funcao(New string(identificador), [parametros_formais.parametro_formal], tipo_resultado.tipo_id_ou_primitivo, bloco)};
	
	parametros_formais {-> parametro_formal*} = abre_parenteses secao_parametros_formais fecha_parenteses {-> [secao_parametros_formais.parametro_formal]} 
		| {empty} {-> []};
		
	secao_parametros_formais {-> parametro_formal*} = {unico} secao_parametro_formal {-> [secao_parametro_formal.parametro_formal]} 
		| {multiplo} secao_parametros_formais ponto_e_virgula secao_parametro_formal {-> [secao_parametro_formal.parametro_formal, secao_parametros_formais.parametro_formal]};
	
	secao_parametro_formal {-> parametro_formal} = {ref} spec_param_ref {-> New parametro_formal.ref([spec_param_ref.ref_formal]) }
		| {valor} spec_param_valor {-> New parametro_formal.valor([spec_param_valor.valor_formal])};
	
	spec_param_valor {-> valor_formal} = identificadores dois_pontos identificador_tipo {-> New valor_formal([identificadores.string], identificador_tipo.tipo_id_ou_primitivo)};
	
	spec_param_ref {-> ref_formal} = var identificadores dois_pontos identificador_tipo {-> New ref_formal([identificadores.string], identificador_tipo.tipo_id_ou_primitivo)};
	
	spec_param_array = {valor} spec_param_array_valor | {variavel} spec_param_array_variavel;
	spec_param_array_valor = identificadores dois_pontos schema_array;
	spec_param_array_variavel = var identificadores dois_pontos schema_array;
	schema_array = array abre_colchete spec_tipos_indices fecha_colchete of identificador_schema_array;
	spec_tipos_indices = {unico} spec_tipo_indice | {multiplo} spec_tipos_indices ponto_e_virgula spec_tipo_indice;
	spec_tipo_indice = [inicio]:identificador ponto_ponto [fim]:identificador dois_pontos identificador_tipo_ordinal;
	identificador_schema_array = {identificador} identificador | {array} schema_array;
	
	tipo_resultado {-> tipo_id_ou_primitivo} = {existente} identificador {-> New tipo_id_ou_primitivo.id(New tipo_identificador(New string(identificador)))}
		| {ordinal} identificador_tipo_ordinal {-> New tipo_id_ou_primitivo.primitivo(identificador_tipo_ordinal.tipo_primitivo)};
	
	sentenca_composta {-> sentenca} = begin sequencia_sentencas end {-> New sentenca.composta([sequencia_sentencas.sentenca])};
	
	sequencia_sentencas {-> sentenca*} = {unico} sentenca {-> [sentenca.sentenca]}
		| {multiplo} sequencia_sentencas ponto_e_virgula sentenca {-> [sentenca.sentenca, sequencia_sentencas.sentenca]};
	
	sentenca {-> sentenca} = {simples} definicao_sentenca_simples {-> definicao_sentenca_simples.sentenca} 
		| {estruturada} definicao_sentenca_estruturada {-> definicao_sentenca_estruturada.sentenca};
	
	label_sentenca = P.label dois_pontos | {empty};
	
	definicao_sentenca {->sentenca} = {simples} definicao_sentenca_simples {-> definicao_sentenca_simples.sentenca} 
		| {estruturada} definicao_sentenca_estruturada {-> definicao_sentenca_estruturada.sentenca} 
		| {estruturada_fechada} definicao_sentenca_estruturada_fechada {-> definicao_sentenca_estruturada_fechada.sentenca};
	
	definicao_sentenca_fechada {-> sentenca} = {simples} definicao_sentenca_simples {-> definicao_sentenca_simples.sentenca}
		| {estruturada} definicao_sentenca_estruturada_fechada {-> definicao_sentenca_estruturada_fechada.sentenca};
	
	definicao_sentenca_simples {-> sentenca} = label_sentenca sentenca_simples {-> sentenca_simples.sentenca};
	
	definicao_sentenca_estruturada {-> sentenca} = label_sentenca sentenca_estruturada {-> sentenca_estruturada.sentenca};
	
	definicao_sentenca_estruturada_fechada {-> sentenca} = label_sentenca sentenca_estruturada_fechada {-> sentenca_estruturada_fechada.sentenca};
	
	sentenca_simples {-> sentenca} = {atribuicao} sentenca_atribuicao  {-> sentenca_atribuicao.sentenca }
		| {procedure} sentenca_proc_funcao {-> sentenca_proc_funcao.sentenca}
		| {goto} sentenca_goto {-> sentenca_goto.sentenca} 
		| {empty} {-> New sentenca.vazia()};
		
	sentenca_atribuicao {-> sentenca} = acesso_variavel atribuicao expressao {-> New sentenca.atribuicao(acesso_variavel.acesso_variavel, expressao.expressao)};
	
	sentenca_proc_funcao {-> sentenca} = identificador parametros_reais {-> New sentenca.procedure(New string(identificador), [parametros_reais.expressao]) };
	
	parametros_reais {-> expressao*} = abre_parenteses definicao_parametros_reais fecha_parenteses {-> [definicao_parametros_reais.expressao]}
		| {empty} {-> []};
	
	definicao_parametros_reais {-> expressao*} = {unico} expressao {-> [expressao.expressao]}
		| {multiplo} definicao_parametros_reais virgula expressao {-> [expressao.expressao, definicao_parametros_reais.expressao]};
	
	sentenca_estruturada {-> sentenca} = {composta} sentenca_composta {-> sentenca_composta.sentenca} 
		| {while} sentenca_while {-> sentenca_while.sentenca}
		| {if} sentenca_if {-> sentenca_if.sentenca};
	
	sentenca_estruturada_fechada {-> sentenca} = {composta} sentenca_composta {-> sentenca_composta.sentenca}
		| {while} sentenca_while_fechada {-> sentenca_while_fechada.sentenca};
	
	sentenca_while {-> sentenca} = while expressao do definicao_sentenca_simples {-> New sentenca.while(expressao.expressao, definicao_sentenca_simples.sentenca) }
		| {estruturada} while expressao do definicao_sentenca_estruturada {-> New sentenca.while(expressao.expressao, definicao_sentenca_estruturada.sentenca) };
	
	sentenca_while_fechada {->sentenca} = while expressao do definicao_sentenca_fechada {-> New sentenca.while(expressao.expressao, definicao_sentenca_fechada.sentenca) };
	
	sentenca_goto {-> sentenca} = goto P.label {-> New sentenca.goto(P.label.numero_sem_sinal) };
	
	sentenca_if {-> sentenca} = if expressao then sentenca {-> New sentenca.if(expressao.expressao, sentenca.sentenca, New sentenca.vazia())} 
		| {else} if expressao then sentenca_if_fechada else sentenca {-> New sentenca.if(expressao.expressao, sentenca_if_fechada.sentenca, sentenca.sentenca)};
		
	sentenca_if_fechada {-> sentenca} = {if} if expressao then [parte_if]:sentenca_if_fechada else sentenca_if_fechada {-> New sentenca.if(expressao.expressao, parte_if.sentenca, sentenca_if_fechada.sentenca)} 
		| definicao_sentenca_fechada {-> definicao_sentenca_fechada.sentenca};
	
	acesso_variavel {-> acesso_variavel} = identificador {-> New acesso_variavel.variavel(New variavel(New string(identificador))) } 
		| {indexada} variavel_indexada {-> New acesso_variavel.var_indexada(variavel_indexada.variavel_indexada)};
		
	variavel_indexada {-> variavel_indexada} = acesso_variavel abre_colchete expressoes fecha_colchete {-> New variavel_indexada(acesso_variavel.acesso_variavel, [expressoes.expressao])};
	
	expressoes {-> expressao*} = {unico} expressao {-> [expressao.expressao]}
		| {multiplo} expressoes virgula expressao {-> [expressao.expressao, expressoes.expressao]};
	
	expressao {-> expressao} = expressao_simples {-> expressao_simples.expressao}
		| {relacional} [esq]:expressao_simples operador_relacional [dir]:expressao_simples {-> New expressao.exp_relacional(operador_relacional.operador_relacional, esq.expressao, dir.expressao)};
	
	expressao_simples {-> expressao} = {unico_com_sinal} sinal+ termo {-> New expressao.signed([sinal.sinal], termo.expressao)} 
		| {unico_sem_sinal} termo {-> termo.expressao}
		| {multiplo_sem_sinal} expressao_simples sinal termo {-> New expressao.exp_bin_arit(New operador_aritmetico_binario.soma_sub(sinal.sinal), expressao_simples.expressao, termo.expressao) } 
		| {multiplo_com_sinal} expressao_simples sinal [sinais_adicionais]:sinal+ termo {-> New expressao.exp_bin_arit(New operador_aritmetico_binario.soma_sub(sinal.sinal), expressao_simples.expressao, New expressao.signed([sinais_adicionais.sinal], termo.expressao)) }
		| {multiplo_sem_sinal_or} expressao_simples or termo {-> New expressao.exp_bin_boolean(New operador_booleano_binario.or(or), expressao_simples.expressao, termo.expressao)} 
		| {multiplo_com_sinal_or} expressao_simples sinal or termo {-> New expressao.exp_bin_boolean(New operador_booleano_binario.or(or), expressao_simples.expressao, New expressao.signed([sinal], termo.expressao))};

	termo {-> expressao} = {unico} fator {-> fator.expressao} 
		| {multiplo_multiplicativo} termo operador_multiplicativo fator {-> New expressao.exp_bin_arit(operador_multiplicativo.operador_aritmetico_binario, termo.expressao, fator.expressao) }
		| {multiplo_and} termo and fator {-> New expressao.exp_bin_boolean(New operador_booleano_binario.and(and), termo.expressao, fator.expressao)};

	fator {-> expressao} = {constante_sem_sinal} constante_sem_sinal {-> New expressao.constante(constante_sem_sinal.constante) }
		| {expressao} abre_parenteses expressao fecha_parenteses {-> expressao.expressao}
		| {not} not fator {-> New expressao.not(fator.expressao)}
		| {funcao_ou_variavel} identificador parametros_reais {-> New expressao.funcao(New string(identificador), [parametros_reais.expressao])}
		| {var_indexada} variavel_indexada {-> New expressao.variavel(New acesso_variavel.var_indexada(variavel_indexada.variavel_indexada))};

	operador_relacional {-> operador_relacional} = {menor} menor {-> New operador_relacional.menor(menor)} 
		| {menor_igual} menor_igual {-> New operador_relacional.menor_igual(menor_igual)}
		| {maior} maior {-> New operador_relacional.maior(maior)}
		| {maior_igual} maior_igual {-> New operador_relacional.maior_igual(maior_igual)}
		| {igual} igual {-> New operador_relacional.igual(igual)}
		| {diferente} diferente {-> New operador_relacional.diferente(diferente)};

	operador_multiplicativo {-> operador_aritmetico_binario} = 
		{multiplicacao} multiplicacao {-> New operador_aritmetico_binario.multiplicacao(multiplicacao)}
		| {divisao} divisao {-> New operador_aritmetico_binario.divisao(divisao)}
		| {div} div {-> New operador_aritmetico_binario.div(div)}
		| {mod} mod {-> New operador_aritmetico_binario.mod(mod)};
	
Abstract Syntax Tree
	program = [id]:P.string [ids]:P.string* bloco;
	string = identificador;
	constante = {inteiro} P.sinal? const_inteiro | {string} const_string | {boolean} const_booleana;
	bloco = numero_sem_sinal* definicao_tipo* declaracao_variavel* declaracao_proc_ou_funcao*;
	numero_sem_sinal = const_inteiro;
	
	definicao_tipo = P.string denotador_tipo;
	denotador_tipo = {primitivo} tipo_primitivo | {id_ou_ordinal} tipo_id_ou_ordinal | {array} tipo_array;
	tipo_primitivo = {integer} integer | {string} T.string | {boolean} boolean;
	tipo_id_ou_ordinal = {id} tipo_identificador | {enumerado} tipo_enumerado | {subrange} tipo_subrange;
	tipo_id_ou_primitivo = {id} tipo_identificador | {primitivo} tipo_primitivo;
	tipo_identificador = P.string;
	tipo_enumerado = P.string*;
	tipo_subrange = [inicio]:constante [fim]:constante;
	tipo_array = tipo_id_ou_ordinal+ denotador_tipo;
	sinal = {mais} soma | {menos} subtracao;
	
	declaracao_variavel = P.string* denotador_tipo;
	
	declaracao_proc_ou_funcao = {procedure} declaracao_procedure | {funcao} declaracao_funcao;
	declaracao_procedure = P.string parametro_formal* bloco;
	parametro_formal = {valor} valor_formal* | {ref} ref_formal*;
	valor_formal = P.string* tipo_id_ou_primitivo;
	ref_formal = P.string* tipo_id_ou_primitivo;
	
	declaracao_funcao = P.string parametro_formal* tipo_id_ou_primitivo bloco;
	
	sentenca = {vazia} | {atribuicao} acesso_variavel expressao | {procedure} P.string expressao* | {goto} numero_sem_sinal
		| {if} expressao [if]:sentenca [else]:sentenca | {while} expressao sentenca | {composta} sentenca*;
	
	acesso_variavel = {variavel} variavel | {var_indexada} variavel_indexada;
	variavel = P.string;
	variavel_indexada = acesso_variavel expressao+;
	
	expressao = {variavel} acesso_variavel | {exp_bin_arit} operador_aritmetico_binario [esq]:expressao [dir]:expressao | {exp_bin_boolean} operador_booleano_binario [esq]:expressao [dir]:expressao
		| {exp_relacional} operador_relacional [esq]:expressao [dir]:expressao | {signed} sinal+ expressao | {not} expressao | {funcao} P.string expressao*
		| {constante} constante;

	operador_aritmetico_binario = {div} div | {mod} mod | {soma_sub} sinal | {multiplicacao} multiplicacao | {divisao} divisao;
	operador_booleano_binario = {and} and | {or} or;
	operador_relacional = {igual} igual | {diferente} diferente | {maior} maior | {maior_igual} maior_igual | {menor} menor | {menor_igual} menor_igual;